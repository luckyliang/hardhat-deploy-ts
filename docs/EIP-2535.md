### [EIP-2535: Diamonds, Multi-Facet Proxy](https://eips.ethereum.org/EIPS/eip-2535)：钻石协议

该提案将钻石标准化，钻石是模块化的智能合约系统，部署后可以升级/扩展，几乎没有大小限制。从技术上讲，**菱形**是一种具有外部功能的合约，这些功能由称为**facets**的合约提供。Facets 是单独的、独立的合约，可以共享内部函数、库和状态变量。

### 执行原理

当在钻石上调用外部函数时，将执行其回退函数。回退函数根据调用数据的前四个字节（称为函数选择器）确定调用哪个面，并使用`delegatecall`.

```solidity
// Find facet for function that is called and execute the
// function if a facet is found and return any value.
fallback() external payable {
  // get facet from function selector
  address facet = selectorTofacet[msg.sig];
  require(facet != address(0));
  // Execute external function from facet using delegatecall and return any value.
  assembly {
    // copy function selector and any arguments
    calldatacopy(0, 0, calldatasize())
    // execute function call using the facet
    let result := delegatecall(gas(), facet, 0, calldatasize(), 0, 0)
    // get any return value
    returndatacopy(0, 0, returndatasize())
    // return any return value or error back to the caller
    switch result
      case 0 {revert(0, returndatasize())}
      default {return (0, returndatasize())}
  }
}
```

智能合约或已部署的 Solidity 库可以是钻石的各个面。仅包含内部函数的 Solidity 库无法部署，也不能成为切面。来自 Solidity 库的内部函数包含在使用它们的方面和合约的字节码中。仅具有内部函数的 Solidity 库对于在构面之间共享内部函数很有用。

#### 添加/替换/删除功能

###  `IDiamond`接口

所有钻石都必须实现该`IDiamond`接口。

在部署钻石期间，必须在`DiamondCut`事件中发出任何不可变函数和添加到钻石的任何外部函数。

**`DiamondCut`任何时候添加、替换或删除外部函数时都必须发出事件。**这适用于所有升级，所有功能更改，在任何时候，无论是否通过`diamondCut`。

```solidity
interface IDiamond {
    enum FacetCutAction {Add, Replace, Remove}
    // Add=0, Replace=1, Remove=2

    struct FacetCut {
        address facetAddress;		//face地址
        FacetCutAction action;
        bytes4[] functionSelectors; //函数签名数组
    }
		
    event DiamondCut(FacetCut[] _diamondCut, address _init, bytes _calldata);
}
```

该`DiamondCut`事件记录了钻石的所有功能变化。

### `IDiamondCut`

菱形包含函数选择器到分面地址的映射。通过修改此映射来**添加/替换/删除功能**。

如果Diamonds`IDiamondCut`在部署后允许修改其函数选择器映射，则应该实现该接口。

该`diamondCut`函数在单个事务中从任意数量的方面更新任意数量的函数。在单个事务中执行所有更改可防止数据损坏，而数据损坏可能发生在通过多个事务完成的升级中。

`diamondCut`被指定用于互操作性的目的。工具、软件和用户界面应该期望并使用标准`diamondCut`功能。

```solidity
interface IDiamondCut is IDiamond {
    /// @notice Add/replace/remove any number of functions and optionally execute
    ///         a function with delegatecall
    /// @param _diamondCut Contains the facet addresses and function selectors
    /// @param _init The address of the contract or facet to execute _calldata
    /// @param _calldata A function call, including function selector and arguments
    ///                  _calldata is executed with delegatecall on _init
    function diamondCut(
        FacetCut[] calldata _diamondCut,
        address _init,
        bytes calldata _calldata
    ) external;
}
```

`_diamondCut`参数是一个`FacetCut`结构数组。

每个`FacetCut`结构都包含一个 facet 地址和以菱形更新的函数选择器数组。

对于每个`FacetCut`结构：

- 如果`action`是`Add`，则将每个`functionSelectors`项目的函数选择器映射更新为`facetAddress`。如果其中任何`functionSelectors`一个具有映射的方面，则改为还原。
- 如果`action`是`Replace`，则将每个`functionSelectors`项目的函数选择器映射更新为`facetAddress`。如果其中任何`functionSelectors`一个的值等于`facetAddress`或未设置选择器，则改为还原。
- 如果`action`是`Remove`，则删除每个`functionSelectors`项目的函数选择器映射。如果`functionSelectors`之前未设置任何一个，则改为还原。

##### 执行中`_calldata`

在添加/替换/删除函数后，`_calldata`参数将使用`delegatecall`on执行`_init`。完成此执行以初始化数据或设置或删除任何需要或在添加、替换和/或删除功能后不再需要的内容。

如果该`_init`值为`address(0)`则`_calldata`跳过执行。在这种情况下`_calldata`可以包含 0 个字节或自定义信息。

### IDiamondLoupe

钻石必须支持通过实现`IDiamondLoupe`接口来检查刻面和功能。

```solidity
// A loupe is a small magnifying glass used to look at diamonds.
// These functions look at diamonds
interface IDiamondLoupe {
    struct Facet {
        address facetAddress;
        bytes4[] functionSelectors;
    }

    /// @notice Gets all facet addresses and their four byte function selectors.
    /// @return facets_ Facet
    function facets() external view returns (Facet[] memory facets_);

    /// @notice Gets all the function selectors supported by a specific facet.
    /// @param _facet The facet address.
    /// @return facetFunctionSelectors_
    function facetFunctionSelectors(address _facet) external view returns (bytes4[] memory facetFunctionSelectors_);

    /// @notice Get all the facet addresses used by a diamond.
    /// @return facetAddresses_
    function facetAddresses() external view returns (address[] memory facetAddresses_);

    /// @notice Gets the facet that supports the given selector.
    /// @dev If facet is not found return address(0).
    /// @param _functionSelector The function selector.
    /// @return facetAddress_ The facet address.
    function facetAddress(bytes4 _functionSelector) external view returns (address facetAddress_);
}
```

钻石必须实现以下内容：

1. 菱形包含回退函数和零个或多个在其中定义的不可变函数。
2. 菱形将功能选择器与刻面相关联。
3. 当在菱形上调用函数时，如果它是直接在菱形中定义的“不可变函数”，则它会立即执行。否则执行菱形的回退功能。回退函数找到与该函数关联的方面，并使用 执行该函数`delegatecall`。如果该函数没有分面，则可以选择执行默认函数。如果函数没有分面，没有默认函数，也没有其他机制来处理它，那么执行将恢复。
4. 每次添加、替换或删除函数时，`DiamondCut`都会发出一个事件来记录它。
5. 钻石实现 DiamondLoupe 接口。
6. 所有不可变函数都必须在`DiamondCut`添加新函数时在事件中发出。如果存在不可变函数，则放大镜函数必须返回有关不可变函数的信息。不可变函数的分面地址是菱形的地址。任何删除或替换不可变函数的尝试都必须还原。

钻石可以实现以下功能：

1. [EIP-165](https://eips.ethereum.org/EIPS/eip-165)的`supportsInterface`. 如果钻石具有该`diamondCut`功能，则用于它的接口 ID 为`IDiamondCut.diamondCut.selector`。用于钻石放大镜界面的界面 ID 是`IDiamondLoupe.facets.selector ^ IDiamondLoupe.facetFunctionSelectors.selector ^ IDiamondLoupe.facetAddresses.selector ^ IDiamondLoupe.facetAddress.selector`。

钻石地址是用户与之交互的地址。钻石地址不变。只有构面地址可以通过使用`diamondCut`函数或其他函数来更改。

















